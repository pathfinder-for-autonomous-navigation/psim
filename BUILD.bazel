load("@rules_cc//cc:defs.bzl", "cc_library")
load("@rules_python//python:defs.bzl", "py_runtime", "py_binary")

load("//bazel:psim_build.bzl", "psim_autocoded_cc_library", "psim_cc_library", "psim_py_extension")


# Let bazel know about our virtual environment.
#
# Also see additional options passed with .bazelrc to ensure we're using the
# virtual environment.
#
# TODO : This is a little hacky and could be cleaned up with toolchains.
py_runtime(
    name = "venv",
    files = glob(["venv/**"], exclude=["venv/**/* *"]),
    interpreter = "venv/bin/python",
    python_version = "PY3",
    visibility = ["//visibility:private"],
)

# Register the autocoder as an executable. This is later consumed by the autocoder
# command. See psim.bzl for more information.
py_binary(
    name = "autocoder",
    srcs = ["//:tools/autocoder.py"],
    srcs_version = "PY3",
    visibility = ["//visibility:private"],
)

# Builds the GNC flight software implementations to be linked in as a dependancy
# of PSim.
cc_library(
    name = "gnc",
    srcs = glob([
        "src/gnc/**/*.hpp", "src/gnc/*.cpp", "src/gnc/**/*.inl",
        "include/gnc/**/*.inl"
    ]),
    hdrs = glob([
        "include/gnc/**/*.hpp", "include/orb/*.h", "include/orb/*.hpp"
    ]),
    includes = ["include"],
    copts = ["-Isrc", "-fvisibility=hidden"],
    linkstatic = True,
    visibility = ["//visibility:public"],
    deps = ["@lin//:lin"],
)

# Builds the core PSim infrastructure.
psim_cc_library(
    name = "core",
    deps = ["@lin//:lin"],
    visibility = ["//visibility:public"],
)

# Builds the PSim truth models
psim_autocoded_cc_library(
    name = "truth",
    deps = ["@lin//:lin", "//:gnc", "//:psim_core"],
    visibility = ["//visibility:public"],
)

# Builds all PSim models deemed "simulation" worthy.
psim_cc_library(
    name = "simulations",
    deps = ["//:psim_core", "//:psim_truth"],
    visibility = ["//visibility:public"],
)

# PSim Python extension
psim_py_extension(
    name = "_psim",
    srcs = glob([
        "src/psim/_psim/**/*.hpp", "src/psim/_psim/**/*.inl",
        "src/psim/_psim/**/*.cpp",
    ]),
    deps = [
        "@variant//:variant", "//:psim_core", "//:psim_simulations",
    ],
    visibility = ["//visibility:public"],
)
